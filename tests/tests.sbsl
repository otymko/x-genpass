
метод Скрипт()
    пер РезультатПрогона = Истина
    знч ТестовыеКейсы = СписокТестов()

    Консоль.Записать("Запущен прогон тестов")
    Консоль.Записать("")

    для ТестовыйКейс из ТестовыеКейсы
        
        попытка
            ТестовыйКейс.Метод() 

            Консоль.Записать("УСПЕШНО: " + ТестовыйКейс.Заголовок) 
        поймать Исключение: Исключение
            РезультатПрогона = Ложь
            Консоль.Записать("ОШИБКА: " + ТестовыйКейс.Заголовок)
            Консоль.ЗаписатьОшибку(Исключение.Описание)
        ;  
    ;

    Консоль.Записать("")
    если РезультатПрогона
        Консоль.ЗаписатьОшибку("Тесты пройдены успешно")
    иначе
        Консоль.ЗаписатьОшибку("Запуск тестов завершился ошибкой")
    ;
;

структура ТестовыйКейс
    обз знч Заголовок: Строка
    обз знч Метод: () -> ничто   
;

метод СписокТестов(): Массив<ТестовыйКейс>
    пер Тесты = новый Массив<ТестовыйКейс>()

    Тесты.Добавить(новый ТестовыйКейс(
            "Тест_ПроверитьЗапускБезАргументов", 
            () -> Тест_ПроверитьЗапускБезАргументов())
    )

    возврат Тесты
;

конст КОМАНДА_ЗАПУСКА_ПРИЛОЖЕНИЯ = "bin/x-genpass"

метод Тест_ПроверитьЗапускБезАргументов()
    пер Процесс = новый ПроцессОс(КОМАНДА_ЗАПУСКА_ПРИЛОЖЕНИЯ, [], Истина)
    Процесс.Запустить()
    Процесс.ОжидатьЗавершения()

    пер Вывод = Процесс.ПолучитьПотокВывода().ПрочитатьКакСтроку()
    знч КодВозврата = Процесс.ПолучитьКодВозврата()
    если КодВозврата != 0
        Консоль.Записать(Вывод)
    ;

    УтверждениеИстинно(КодВозврата == 0)
    УтверждениеИстинно(Вывод.Длина() > 0)
;

/////////////////////////////////////////////////////////////////////////////
исключение ИсключениеУтверждения   
;

метод УтверждениеИстинно(Значение: Булево, Сообщение: Строка? = Неопределено)
    если не Значение
        если Сообщение == Неопределено
            знч Ошибка = "Ожидамое значение: %{Истина}, получено: %Значение"
            выбросить новый ИсключениеУтверждения(Ошибка) 
        иначе
            выбросить новый ИсключениеУтверждения(Сообщение) 
        ;     
    ;
;

метод УтверждениеРавно(
        ОжидаемоеЗначение: неизвестно, 
        ТекущееЗначение: неизвестно, 
        Сообщение: Строка? = Неопределено)

    если ОжидаемоеЗначение != ТекущееЗначение
        если Сообщение == Неопределено
            знч Ошибка = 
                "Ожидаемое значение: %{ОжидаемоеЗначение.ВСтроку()} (%{ОжидаемоеЗначение.ПолучитьТип().ВСтроку()})" 
                + Символы.НОВАЯ_СТРОКА
                + "Текущее значение: %{ТекущееЗначение.ВСтроку()} (%{ТекущееЗначение.ПолучитьТип().ВСтроку()})"
            выбросить новый ИсключениеУтверждения(Ошибка)
        иначе
            выбросить новый ИсключениеУтверждения(Сообщение)    
        ;
    ;

;