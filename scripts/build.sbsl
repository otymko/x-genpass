метод Скрипт()
    знч ВременныйКаталог = Файлы.СоздатьВременныйКаталог(УдалитьПослеОкончанияРаботы = Истина)
    знч Архив = новый Файл("x-genpass.sbslx", ВременныйКаталог.Путь)

    исп ПотокЗаписи = Архив.ОткрытьПотокЗаписи()
    пер ЗаписьZip = новый ЗаписьZip(ПотокЗаписи)
     
    знч Каталог = новый Файл(Скрипт.ПутьТекущегоСкрипта).Каталог!.Каталог
    для Файл из Каталог!.Дочерние
        если Файл.Имя == "src" или Файл.Имя == "bin"
            ЗапаковатьКаталог(ЗаписьZip, Файл, Файл.Имя)
        иначе если Файл.Имя == "README.md"
            ЗаписьZip.Добавить(Файл.ОткрытьПотокЧтения(), Файл.Имя)
        ;

    ;
 
    ЗаписьZip.Записать()

    знч ФайлНазначения = новый Файл(Архив.Имя, Каталог!)
    Файлы.Переместить(Архив, ФайлНазначения)
;

метод ЗапаковатьКаталог(ЗаписьZip: ЗаписьZip,Каталог: Файл, ВнутреннийКаталог: Строка)
    для Файл из Каталог.Дочерние
        если Файл.ЯвляетсяКаталогом()
            ЗапаковатьКаталог(ЗаписьZip, Файл, 
                ВнутреннийКаталог + (ВнутреннийКаталог.Пусто() ? "" : "/") + Файл.Имя)
        иначе
            ЗаписьZip.Добавить(Файл.ОткрытьПотокЧтения(), ВнутреннийКаталог + "/" + Файл.Имя)
        ;  
    ;
;